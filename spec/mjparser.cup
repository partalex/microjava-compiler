package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected = false;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, BREAK, ELSE, CONST, IF, NEW, PRINT, READ, RETURN, VOID, CONTINUE, FOR, NAMESPACE;
terminal PLUS, MINUS, MUL, DIV, MOD;
terminal EQUALSTO, DIFFERENT, GREATER, EGREATER, LESS, ELESS, AND, OR;
terminal EQUAL, PLUSPLUS, MINUSMINUS;
terminal SEMI, DOUBLEDOT, COMMA, LPAREN, RPAREN, LSQBRACE, RSQBRACE, LBRACE, RBRACE;
terminal LAMBDA, DOUBLECOLON;
terminal String BOOL;
terminal String CHAR;
terminal Integer NUMBER;
terminal String IDENT;

//nonterminal Namespace; // defined
nonterminal NamespaceMany;
//nonterminal Program; // defined
nonterminal ConstDeclOptMany;
nonterminal VarDeclOptMany;
nonterminal MethodDeclOptMany;
nonterminal ConstDecl;
nonterminal ConstList;
//nonterminal ConstDeclPart; // defined
//nonterminal VarDeclPart; // defined
nonterminal VarDecl;
nonterminal VarDeclOpt;
nonterminal FormParsOptOne;
//nonterminal MethodDecl; // defined
nonterminal FormPars;
nonterminal OptMinus;
nonterminal OptActPars;
//nonterminal Type; // defined
nonterminal DesignatorStatementOpt;
nonterminal OptPrint;
nonterminal Statement;
nonterminal StatementMany;
//nonterminal Term; // defined
nonterminal DesignatorStatePart;
nonterminal DesignatorStatement;
nonterminal ActPars;
nonterminal ActPartsOptional;
nonterminal OptionalArray;
//nonterminal Condition; // defined
//nonterminal CondTerm; // defined
//nonterminal CondFact; // defined
//nonterminal Expr; // defined
nonterminal OptExpr;
nonterminal OptFactorParenPars;
//nonterminal ConstVal; // defined
//nonterminal Factor; // defined
//nonterminal Designator; // defined
//nonterminal OptDesignatorPart; // defined
nonterminal Label;
nonterminal Assignop;
nonterminal Relop;
nonterminal Addop;
nonterminal Mulop;
nonterminal VarDeclList;
nonterminal FormParsList;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ConstVal;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;
nonterminal rs.etf.pp1.symboltable.concepts.Struct AddTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct MethodType;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFactOptional;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Namespace;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj OptDesignatorPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj DesigPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstDeclPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDeclPart;

nonterminal If;
nonterminal Else;

precedence left Else;
//precedence left ELSE;
//precedence left IF;

If					::= (If) IF;
Else 				::= (Else) ELSE;

Namespace 		    ::= (Namespace) NAMESPACE IDENT:namespaceName LBRACE ConstDeclOptMany VarDeclOptMany LBRACE MethodDeclOptMany RBRACE RBRACE;

NamespaceMany       ::= (NamespaceManyClass) NamespaceMany Namespace
                    |   (NamespaceManyEmptyClass) /* epsilon */;

// Done
Program 		    ::= (Program) PROG IDENT:programName NamespaceMany ConstDeclOptMany VarDeclOptMany LBRACE MethodDeclOptMany RBRACE;

// Done
MethodDeclOptMany	::= (MethodDeclOptManyClass) MethodDeclOptMany MethodDecl
					|	(MethodDeclOptEmptyClass) /* epsilon */;
// Done
OptionalArray		::= (OptionalArrayClass) LSQBRACE RSQBRACE
					|	(OptionalArrayEmptyClass) /* epsilon */;

// Done
VarDeclPart         ::= (VarDeclPart) Type IDENT:name OptionalArray;

// Done
VarDeclList         ::= (VarDeclListOneClass) VarDeclPart
                    |	(VarDeclListManyClass) VarDeclList COMMA VarDeclPart
                    |   error SEMI:l {: parser.report_error("Sintaksa greska deklaracije globalne promenljive na liniji " + lleft, null); :};

// Done
ConstDeclOptMany    ::= (ConstDeclOptManyClass) ConstList
					|	(ConstDeclOptNoClass) /* epsilon */;
// Done
ConstDecl           ::= (ConstDecl) CONST Type ConstDeclPart SEMI;

// Done
ConstDeclPart       ::= (ConstDeclPart) IDENT:name EQUAL ConstVal:value;

// Done
ConstList           ::= (ConstListManyClass) ConstList COMMA ConstDeclPart
                    |   (ConstListOneClass) ConstDeclPart;

// Done
VarDecl             ::= (VarDecl) VarDeclPart SEMI;

// Done
VarDeclOpt          ::= (VarDeclOptManyClass) VarDeclList
                    |   (VarDeclOptEmptyClass) /* epsilon */;

// Done
MethodType			::= (MethodTypeVoidClass) VOID
					|	(MethodTypeTypeClass) Type;

// Done
MethodDecl          ::= (MethodDecl) MethodType IDENT:methodName LPAREN FormParsOptOne RPAREN VarDeclOpt LBRACE StatementMany RBRACE;

// Done
FormParsOptOne      ::= (FormParsOptOneClass) FormParsList
                    |   (FormParsOptOneEmptyClass) /* epsilon */;

// Done
FormParsList        ::= (FormParsListManyClass) FormParsList COMMA FormPars
                    |   (FormParsListOneClass) FormPars;

// Done
FormPars            ::= (FormPars) Type IDENT:name OptionalArray;

// Done
Type                ::= (TypeNamespaceClass) IDENT:namespace DOUBLECOLON IDENT:type
                    |	(TypeClass) IDENT:type;

// Done
StatementMany       ::= (StatementManyManyClass) StatementMany Statement
                    |   (StatementEmptyClass) /* epsilon */;

// Done
OptExpr				::= (OptExprOneClass) Expr
					| 	(OptExprEmptyClass);

Statement           ::= (StatementDesignatorClass) DesignatorStatement SEMI
                    |   (StatementIfClass) If LPAREN Condition RPAREN Statement
                    |   (StatementIfElseClass) If LPAREN Condition RPAREN Statement Else Statement
                    |   (StatementBreakClass) BREAK SEMI // Done
                    |   (StatementContinueClass) CONTINUE SEMI // Done
                    |   (StatementReturnClass) RETURN OptExpr SEMI // Done
                    |   (StatementReadClass) READ LPAREN Designator RPAREN SEMI
                    |   (StatementPrintClass) PRINT LPAREN Expr OptPrint RPAREN SEMI // Done
                    |   (StatementForClass) FOR LPAREN DesignatorStatementOpt SEMI CondFactOptional SEMI DesignatorStatementOpt RPAREN // Done
                    |   (StatementManyClass) LBRACE StatementMany RBRACE; // Done

CondFactOptional    ::= (CondFactOptionalOneClass) CondFact
                    |	(CondFactOptionalEmptyClass) /* epsilon */;

// Done
OptPrint	        ::= (OptPrintOneClass) COMMA NUMBER
					|	(OptPrintEmptyClass) /* epsilon */;

// Done
DesignatorStatePart ::= (DesignatorStatePartManyClass) DesignatorStatePart Designator COMMA
                    |   (DesignatorStatePartOneClass) DesignatorStatePart COMMA
                    |   (DesignatorStatePartEmptyClass) /* epsilon */;

DesignatorStatement ::= (DesignatorStatementAssignClass) Designator Assignop Expr // Done
                    |   (DesignatorStatementParamsClass) Designator LPAREN ActPartsOptional RPAREN // Done
                    |   (DesignatorStatementPlusClass) Designator PLUSPLUS // Done
                    |   (DesignatorStatementMinusClass) Designator MINUSMINUS // Done
                    |   (DesignatorStatementManyClass) LSQBRACE DesignatorStatePart MUL Designator RSQBRACE Assignop Designator;

// Done
ActPartsOptional	::= (ActPartsOptionalOneClass) ActPars
					|	(ActPartsOptionalEmptyClass) /* epsilon */;

// Done
ActPars             ::= (ActParsManyClass) ActPars COMMA Expr
                    |   (ActParsOneClass) Expr;

// Done
Condition           ::= (ConditionManyClass) Condition OR CondTerm
                    |   (ConditionOneClass) CondTerm;

// Done
CondTerm            ::= (CondTermManyClass) CondTerm AND CondFact
                    |   (CondTermOneClass) CondFact;

// Done
CondFact            ::= (CondFactManyClass) Expr Relop Expr
                    |   (CondFactOneClass) Expr;

// Done
AddTerm				::= (AddTermManyClass) AddTerm Addop Term
					|	(AddTermEmptyClass) /* epsilon */;

// Done
Expr                ::= (Expr) OptMinus Term AddTerm;

// Done
OptMinus 			::= (OptMin) MINUS
					|	(NoMinus) /* epsilon */;

// Done
Term                ::= (TermManyClass) Term Mulop Factor
                    |   (TermOneClass) Factor;

// Done
ConstVal 			::= (ConstValNumClass) NUMBER
					|	(ConstValCharClass) CHAR
					|	(ConstValBooCllass) BOOL;

// Done
Factor              ::= (FactorParenParsClass) Designator OptFactorParenPars
                    |   (FactorConstValClass) ConstVal
                    |   (FactorNewTypeExprClass) NEW Type LSQBRACE Expr RSQBRACE
                    |   (FactorParenExprClass) LPAREN Expr RPAREN;

// Done
OptFactorParenPars  ::= (OptFactorParenParsClass) LPAREN ActPars RPAREN
                    |   (OptFactorParenClass) LPAREN RPAREN
                    |   (OptFactorEmptyClass) /* epsilon */;

// Done
OptActPars          ::= (OptActParsOneClass) ActPars
                    |   (OptActParsEmptyClass) /* epsilon */;

// Done
Designator          ::= (Designator) Type IDENT:name OptDesignatorPart;

// Done
OptDesignatorPart   ::= (OptDesignatorPartManyClass) OptDesignatorPart DesigPart
                    |   (OptDesignatorPartEmptyClass) /* epsilon */;

// Done
DesigPart			::= (DesigPart) LSQBRACE Expr RSQBRACE;

//Done
Label               ::= (Label) IDENT:name;

//Done
Assignop		    ::= (Assignop) EQUAL
                    |	error SEMI:l {: parser.report_error("Sintaksa greska konstrukcije iskaza dodele na liniji " + lleft, null); :};

//Done
Relop				::= (RelopEqualstoClass) EQUALSTO
					|	(RelopDifferentClass) DIFFERENT
					|	(RelopLessClass) LESS
					|	(RelopElessClass) ELESS
					|	(RelopGreaterClass) GREATER
					|	(RelopEgreaterClass) EGREATER;

//Done
Addop				::= (AddopPlusClass) PLUS
					|	(AddopMinusClass) MINUS;

//Done
Mulop				::= (MulopMulClass) MUL
					| 	(MulopDivClass) DIV
					|	(MulopModClass) MOD;
