package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected = false;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, BREAK, ELSE, CONST, IF, NEW, PRINT, READ, RETURN, VOID, CONTINUE, FOR, NAMESPACE;
terminal PLUS, MINUS, MUL, DIV, MOD;
terminal EQUALSTO, DIFFERENT, GREATER, EGREATER, LESS, ELESS, AND, OR;
terminal EQUAL, PLUSPLUS, MINUSMINUS;
terminal SEMI, DOUBLEDOT, COMMA, LPAREN, RPAREN, LSQBRACE, RSQBRACE, LBRACE, RBRACE;
terminal LAMBDA, DOUBLECOLON;
terminal String BOOL;
terminal String CHAR;
terminal Integer NUMBER;
terminal String IDENT;

nonterminal NamespaceOpt;
nonterminal ConstDeclOpt;
nonterminal MethodDeclOpt;
nonterminal ConstDecl;
nonterminal ConstList;
nonterminal VarDecl;
nonterminal VarDeclOpt;
nonterminal FormParsOpt;
nonterminal FormPars;
nonterminal MinusOpt;
nonterminal ActParsOpt;
nonterminal DesignStmOpt;
nonterminal OptPrint;
nonterminal Statement;
nonterminal StatementOpt;
nonterminal DesignStmPart;
nonterminal DesignatorStm;
nonterminal ActPars;
nonterminal ActPartsOpt;
nonterminal ArrayOpt;
nonterminal ExprOpt;
nonterminal FactorParenParsOpt;
nonterminal Assignop;
nonterminal Relop;
nonterminal Addop;
nonterminal Mulop;
nonterminal VarDeclList;
nonterminal FormParsList;
nonterminal StatementCondition;
nonterminal For;
nonterminal CondStart;
nonterminal CondEnd;
nonterminal ForEnd;
nonterminal GlobalEnd;
nonterminal DesignStmManyStart;
nonterminal NamespaceEnd;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct MethodType;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ConstVal;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;
nonterminal rs.etf.pp1.symboltable.concepts.Struct AddTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFactOpt;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Namespace;
nonterminal rs.etf.pp1.symboltable.concepts.Obj NamespaceOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MatrixOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ArraySize;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstDeclPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDeclPart;


nonterminal If;
nonterminal Else;

precedence left ELSE;

// Done
Program 		    ::= (Program) PROG IDENT:name NamespaceOpt NamespaceEnd ConstDeclOpt VarDeclOpt GlobalEnd MethodDeclOpt RBRACE;

// Done
NamespaceEnd        ::= (NamespaceEnd) /* epsilon */;

// Done
GlobalEnd		    ::= (GlobalEnd) LBRACE;

// Done
Namespace 		    ::= (Namespace) NAMESPACE IDENT:name LBRACE ConstDeclOpt VarDeclOpt LBRACE MethodDeclOpt RBRACE RBRACE;  // TODO

// Done
NamespaceOpt        ::= (NamespaceMany) NamespaceOpt Namespace
                    |   (NamespaceZero) /* epsilon */;

// Done
MethodDeclOpt	    ::= (MethodDeclMany) MethodDeclOpt MethodDecl
					|	(MethodDeclZero) /* epsilon */;

// Done
ArrayOpt		    ::= (ArrayOne) LSQBRACE RSQBRACE
					|	(ArrayZero) /* epsilon */;

// Done
VarDeclPart         ::= (VarDeclPart) IDENT:name ArrayOpt;

// Done
VarDeclList         ::= (VarDeclListMany) VarDeclList COMMA VarDeclPart
                    |	(VarDeclListOne) VarDeclPart
                    |   error SEMI:l {: parser.report_error("Sintaksa greska deklaracije globalne promenljive na liniji " + lleft, null); :};

// Done
ConstDeclOpt        ::= (ConstDeclMany) ConstDeclOpt ConstDecl
					|	(ConstDeclZero) /* epsilon */;

// Done
ConstDecl           ::= (ConstDecl) CONST Type ConstDeclPart SEMI;

// Done
ConstList           ::= (ConstListMany) ConstList COMMA ConstDeclPart
                    |   (ConstListOne) ConstDeclPart;

// Done
ConstDeclPart       ::= (ConstDeclPart) IDENT:name EQUAL ConstVal:value;

// Done
VarDecl             ::= (VarDecl) Type VarDeclList SEMI;

// Done
VarDeclOpt          ::= (VarDeclMany) VarDeclOpt VarDecl
                    |   (VarDeclZero) /* epsilon */;

// Done
MethodType			::= (MethodTypeVoid) VOID
					|	(MethodTypeType) Type;

// Done
MethodDecl          ::= (MethodDecl) MethodType IDENT:name LPAREN FormParsOpt RPAREN VarDeclOpt LBRACE StatementOpt RBRACE;

// Done
FormParsOpt         ::= (FormParsOne) FormParsList
                    |   (FormParsZero) /* epsilon */;

// Done
FormParsList        ::= (FormParsListMany) FormParsList COMMA FormPars
                    |   (FormParsListOne) FormPars;

// Done
FormPars            ::= (FormPars) Type IDENT:name ArrayOpt;

// Done
Type                ::= (Type) IDENT:name;

// Done
StatementOpt        ::= (StatementMany) StatementOpt Statement
                    |   (StatementZero) /* epsilon */;

// Done
ExprOpt				::= (ExprOne) Expr
					| 	(ExprZero) /* epsilon */;

// Done
Statement           ::= (StatementDesignator) DesignatorStm SEMI
                    |   (StatementIf) If StatementCondition Statement
                    |   (StatementIfElse) If StatementCondition Statement Else Statement
                    |   (StatementBreak) BREAK SEMI
                    |   (StatementContinue) CONTINUE SEMI
                    |   (StatementReturn) RETURN ExprOpt SEMI
                    |   (StatementRead) READ LPAREN Designator RPAREN SEMI
                    |   (StatementPrint) PRINT LPAREN Expr OptPrint RPAREN SEMI
                    |   (StatementFor) For LPAREN DesignStmOpt CondStart CondFactOpt SEMI DesignStmOpt CondEnd Statement ForEnd
                    |   (StatementMany) LBRACE StatementOpt RBRACE;

// Done
StatementCondition  ::= (StatementCondition) LPAREN Condition RPAREN;

// Done
DesignStmOpt        ::= (DesignStmMany) DesignStmOpt COMMA DesignatorStm
                    |	(DesignStmOne) DesignatorStm
                    |	(DesignStmZero) /* epsilon */;

// Done
If					::= (If) IF;

// Done
Else 				::= (Else) ELSE;

// Done
For 				::= (For) FOR;

// Done
ForEnd              ::= (ForEnd) /* epsilon */;

// Done
CondStart 	        ::= (CondStart) SEMI;

// Done
CondEnd             ::= (CondEnd) RPAREN;

// Done
CondFactOpt         ::= (CondFactOne) CondFact
                    |	(CondFactZero) /* epsilon */;

// Done
OptPrint	        ::= (PrintOne) COMMA NUMBER
					|	(PrintZero) /* epsilon */;

// Done
DesignStmPart       ::= (DesignStmPartMany) DesignStmPart Designator COMMA
                    |   (DesignStmPartOne) DesignStmPart COMMA
                    |   (DesignStmPartZero) /* epsilon */;

// Done
DesignatorStm       ::= (DesignStmAssign) Designator Assignop Expr
                    |   (DesignStmParamslass) Designator LPAREN ActPartsOpt RPAREN
                    |   (DesignStmPlus) Designator PLUSPLUS
                    |   (DesignStmMinus) Designator MINUSMINUS
                    |   (DesignStmMany) DesignStmManyStart DesignStmPart MUL Designator RSQBRACE Assignop Designator;

// Done
DesignStmManyStart  ::= (DesignStmManyStart) LSQBRACE;

// Done
ActPartsOpt	        ::= (ActPartsOne) ActPars
					|	(ActPartsZero) /* epsilon */;

// Done
ActPars             ::= (ActParsMany) ActPars COMMA Expr
                    |   (ActParsOne) Expr;

// Done
Condition           ::= (ConditionMany) Condition OR CondTerm
                    |   (ConditionOne) CondTerm;

// Done
CondTerm            ::= (CondTermManyClass) CondTerm AND CondFact
                    |   (CondTermOneClass) CondFact;

// Done
CondFact            ::= (CondFactMany) Expr Relop Expr
                    |   (CondFactOne) Expr;

// Done
AddTerm				::= (AddTermMany) AddTerm Addop Term
					|	(AddTermZero) /* epsilon */;

// Done
Expr                ::= (Expr) MinusOpt Term AddTerm;

// Done
MinusOpt 			::= (Minus) MINUS
					|	(NoMinus) /* epsilon */;

// Done
Term                ::= (TermMany) Term Mulop Factor
                    |   (TermOne) Factor;

// Done
ConstVal 			::= (ConstValNum) NUMBER
					|	(ConstValChar) CHAR
					|	(ConstValBoo) BOOL;

// Done
Factor              ::= (FactorParenPars) Designator FactorParenParsOpt
                    |   (FactorConstVal) ConstVal
                    |   (FactorNewTypeExpr) NEW Type LSQBRACE Expr RSQBRACE
                    |   (FactorParenExpr) LPAREN Expr RPAREN;

// Done
FactorParenParsOpt  ::= (ParenPars) LPAREN ActParsOpt RPAREN
                    |   (ParenParsZero) /* epsilon */;

// Done
ActParsOpt          ::= (ActParsOne) ActPars
                    |   (ActParsZero) /* epsilon */;

// Done
NamespaceOpt        ::= (WithNamespace) IDENT:namespace DOUBLECOLON IDENT:name
                    |   (NoNamespace) IDENT:name;

// Done
Designator          ::= (Designator) NamespaceOpt MatrixOpt;

// Done
MatrixOpt           ::= (MatrixMany) MatrixOpt ArraySize
                    |   (MatrixZero) /* epsilon */;

// Done
ArraySize			::= (ArraySize) LSQBRACE Expr RSQBRACE;

// Done
Assignop		    ::= (Assignop) EQUAL
                    |	error SEMI:l {: parser.report_error("Sintaksa greska dodele na liniji " + lleft, null); :};

// Done
Relop				::= (RelopEqualsto) EQUALSTO
					|	(RelopDifferent) DIFFERENT
					|	(RelopLess) LESS
					|	(RelopEless) ELESS
					|	(RelopGreater) GREATER
					|	(RelopEgreater) EGREATER;

// Done
Addop				::= (AddopPlus) PLUS
					|	(AddopMinus) MINUS;

// Done
Mulop				::= (MulopMul) MUL
					| 	(MulopDiv) DIV
					|	(MulopMod) MOD;
