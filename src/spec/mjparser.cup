package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected = false;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, BREAK, ELSE, CONST, IF, NEW, PRINT, READ, RETURN, VOID, CONTINUE, FOR, NAMESPACE;
terminal PLUS, MINUS, MUL, DIV, MOD;
terminal EQUALSTO, DIFFERENT, GREATER, EGREATER, LESS, ELESS, AND, OR;
terminal EQUAL, PLUSPLUS, MINUSMINUS;
terminal SEMI, DOUBLEDOT, COMMA, LPAREN, RPAREN, LSQBRACE, RSQBRACE, LBRACE, RBRACE;
terminal LAMBDA, DOUBLECOLON;
terminal String BOOL;
terminal String CHAR;
terminal Integer NUMBER;
terminal String IDENT;

// modificatiion
terminal MAX;

nonterminal ConstDeclOpt;
nonterminal MethodDeclOpt;
nonterminal ConstDecl;
nonterminal ConstList;
nonterminal VarDecl;
nonterminal VarDeclOpt;
nonterminal FormParsOpt;
nonterminal FormPars;
nonterminal ActParsOpt;
nonterminal DesignStmOpt;
nonterminal PrintOpt;
nonterminal Statement;
nonterminal StatementOpt;
nonterminal DesignStmPart;
nonterminal DesignatorStm;
nonterminal ActPars;
nonterminal ActPartsOpt;
nonterminal ArrayOpt;
nonterminal ExprOpt;
nonterminal FactorParenParsOpt;
nonterminal Assignop;
nonterminal RelOp;
nonterminal AddOp;
nonterminal MulOp;
nonterminal VarDeclList;
nonterminal FormParsList;
nonterminal StatementCondition;
nonterminal For;
nonterminal ForCondStart;
nonterminal ForCondEnd;
nonterminal DesignStmManyStart;

nonterminal LabelStart;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct MethodType;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ConstVal;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFactOpt;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Namespace;
nonterminal rs.etf.pp1.symboltable.concepts.Obj NamespaceName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj NamespaceOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MatrixOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ArraySize;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstDef;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDeclPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Scope;

// modificatiion
nonterminal If;
nonterminal Else;

precedence left ELSE;

Program 		    ::= (Program) PROG ProgName NamespaceOpt ConstDeclOpt VarDeclOpt LBRACE MethodDeclOpt RBRACE;

ProgName 			::= (ProgName) IDENT:name;

NamespaceOpt        ::= (NamespaceMany) NamespaceOpt Namespace
                    |   (NamespaceZero) /* epsilon */;

Namespace 		    ::= (Namespace) NAMESPACE NamespaceName LBRACE ConstDeclOpt VarDeclOpt LBRACE MethodDeclOpt RBRACE RBRACE;

NamespaceName       ::= (NamespaceName) IDENT:name;

ConstDeclOpt        ::= (ConstDeclMany) ConstDeclOpt ConstDecl
					|	(ConstDeclZero) /* epsilon */;

ConstDecl           ::= (ConstDecl) CONST Type ConstDef SEMI;

ConstList           ::= (ConstListMany) ConstList COMMA ConstDef
                    |   (ConstListOne) ConstDef;

ConstDef            ::= (ConstDef) IDENT:name EQUAL ConstVal:value;

VarDecl             ::= (VarDecl) Type VarDeclList SEMI;

VarDeclList         ::= (VarDeclListMany) VarDeclList COMMA VarDeclPart
                    |	(VarDeclListOne) VarDeclPart
                    |   error SEMI:l {: parser.report_error("Global variable declaration syntax error on line " + lleft, null); :};

VarDeclPart         ::= (VarDeclPart) IDENT:name ArrayOpt;

ArrayOpt		    ::= (ArrayOne) LSQBRACE RSQBRACE
					|	(ArrayZero) /* epsilon */;

VarDeclOpt          ::= (VarDeclMany) VarDeclOpt VarDecl
                    |   (VarDeclZero) /* epsilon */;

MethodDeclOpt	    ::= (MethodDeclMany) MethodDeclOpt MethodDecl
					|	(MethodDeclZero) /* epsilon */;

MethodType			::= (MethodVoid) VOID
					|	(MethodTType) Type;

MethodDecl          ::= (MethodDecl) MethodType MethodName LPAREN FormParsOpt RPAREN VarDeclOpt LBRACE StatementOpt RBRACE;

MethodName          ::= (MethodName) IDENT:name;

FormParsOpt         ::= (FormParsOne) FormParsList
                    |   (FormParsZero) /* epsilon */;

FormParsList        ::= (FormParsListMany) FormParsList COMMA FormPars
                    |   (FormParsListOne) FormPars;

FormPars            ::= (FormPars) Type IDENT:name ArrayOpt;

Type                ::= (Type) IDENT:name;

StatementOpt        ::= (StatementOptMany) StatementOpt Statement
                    |   (StatementZero) /* epsilon */;

ExprOpt				::= (ExprOne) Expr
					| 	(ExprZero) /* epsilon */;

Statement           ::= (StatementDesignator) DesignatorStm SEMI
                    |   (StatementIf) If StatementCondition Statement
                    |   (StatementIfElse) If StatementCondition Statement Else Statement
                    |   (StatementBreak) BREAK SEMI
                    |   (StatementContinue) CONTINUE SEMI
                    |   (StatementReturn) RETURN ExprOpt SEMI
                    |   (StatementRead) READ LPAREN Designator RPAREN SEMI
                    |   (StatementPrint) PRINT LPAREN Expr PrintOpt RPAREN SEMI
                    |   (StatementFor) For LPAREN DesignStmOpt ForCondStart CondFactOpt SEMI DesignStmOpt ForCondEnd Statement
                    |   (StatementMany) LBRACE StatementOpt RBRACE
                    // modificatiion
                    |   (StatementGoto) GOTO IDENT:name SEMI
                    |   (StatementLabel) LabelStart IDENT:name DOUBLEDOT Statement
                    ;


StatementCondition  ::= (StatementCondition) LPAREN Condition RPAREN;

DesignStmOpt        ::= (DesignStmOptMany) DesignStmOpt COMMA DesignatorStm
                    |	(DesignStmOne) DesignatorStm
                    |	(DesignStmZero) /* epsilon */;

If					::= (If) IF;

Else 				::= (Else) ELSE;

For 				::= (For) FOR;

ForCondStart 	    ::= (ForCondStart) SEMI;

ForCondEnd          ::= (ForCondEnd) RPAREN;

CondFactOpt         ::= (CondFactOptOne) CondFact
                    |	(CondFactZero) /* epsilon */;

PrintOpt	        ::= (PrintOne) COMMA NUMBER
					|	(PrintZero) /* epsilon */;

DesignStmPart       ::= (DesignStmPartMany) DesignStmPart Designator COMMA
                    |   (DesignStmPartOne) DesignStmPart COMMA
                    |   (DesignStmPartZero) /* epsilon */;

DesignatorStm       ::= (DesignStmAssign) Designator Assignop Expr
                    |   (DesignStmParen) Designator LPAREN ActPartsOpt RPAREN
                    |   (DesignStmPlus) Designator PLUSPLUS
                    |   (DesignStmMinus) Designator MINUSMINUS
                    |   (DesignStmMany) DesignStmManyStart DesignStmPart MUL Designator RSQBRACE Assignop Designator;

DesignStmManyStart  ::= (DesignStmManyStart) LSQBRACE;

ActPartsOpt	        ::= (ActPartsOne) ActPars
					|	(ActPartsZero) /* epsilon */;

ActPars             ::= (ActParsMany) ActPars COMMA Expr
                    |   (ActParsOne) Expr;

Condition           ::= (ConditionMany) Condition OR CondTerm
                    |   (ConditionOne) CondTerm;

CondTerm            ::= (CondTermMany) CondTerm AND CondFact
                    |   (CondTermOne) CondFact;

CondFact            ::= (CondFactMany) Expr RelOp Expr
                    |   (CondFactOne) Expr;

Expr                ::= (ExprAddOp) Expr AddOp Term
         			|   (ExprMinus) MINUS Term
         			|   (ExprTerm) Term;

Term                ::= (TermMany) Term MulOp Factor
                    |   (TermOne) Factor;

ConstVal 			::= (ConstNum) NUMBER
					|	(ConstChar) CHAR
					|	(ConstBool) BOOL;

Factor              ::= (FactorParenPars) Designator FactorParenParsOpt
                    |   (FactorConstVal) ConstVal
                    |   (FactorNewArray) NEW Type LSQBRACE Expr RSQBRACE
                    |   (FactorParenExpr) LPAREN Expr RPAREN
                    |   (FactorMax) MAX Designator;

FactorParenParsOpt  ::= (ParenPars) LPAREN ActParsOpt RPAREN
                    |   (ParenParsZero) /* epsilon */;

ActParsOpt          ::= (ActParsOptOne) ActPars
                    |   (ActParsZero) /* epsilon */;

Scope               ::= (ScopeNamespace) IDENT:namespace DOUBLECOLON IDENT:name
                    |   (ScopeLocal) IDENT:name;

Designator          ::= (Designator) Scope MatrixOpt;

MatrixOpt           ::= (MatrixMany) MatrixOpt ArraySize
                    |   (MatrixZero) /* epsilon */;

ArraySize			::= (ArraySize) LSQBRACE Expr RSQBRACE;

Assignop		    ::= (Assignop) EQUAL
                    |	error SEMI:l {: parser.report_error("Syntax assignment error on line " + lleft, null); :};

RelOp				::= (RelOpEqualsTo) EQUALSTO
					|	(RelOpDifferent) DIFFERENT
					|	(RelOpLess) LESS
					|	(RelOpEless) ELESS
					|	(RelOpGreater) GREATER
					|	(RelOpEgreater) EGREATER;

AddOp				::= (AddOpPlus) PLUS
					|	(AddOpMinus) MINUS;

MulOp				::= (MulOpMul) MUL
					| 	(MulOpDiv) DIV
					|	(MulOpMod) MOD;
