package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected = false;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, BREAK, ELSE, CONST, IF, NEW, PRINT, READ, RETURN, VOID, CONTINUE, FOR, NAMESPACE;
terminal PLUS, MINUS, MUL, DIV, MOD;
terminal EQUALSTO, DIFFERENT, GREATER, EGREATER, LESS, ELESS, AND, OR;
terminal EQUAL, PLUSPLUS, MINUSMINUS;
terminal SEMI, DOUBLEDOT, COMMA, LPAREN, RPAREN, LSQBRACE, RSQBRACE, LBRACE, RBRACE;
terminal LAMBDA, DOUBLECOLON;
terminal String BOOL;
terminal String CHAR;
terminal Integer NUMBER;
terminal String IDENT;

nonterminal NamespaceMany;
nonterminal ConstDeclOpt;
nonterminal MethodDeclOptMany;
nonterminal ConstDecl;
nonterminal ConstList;
nonterminal VarDecl;
nonterminal VarDeclOpt;
nonterminal FormParsOpt;
nonterminal FormPars;
nonterminal OptMinus;
nonterminal ActParsOpt;
nonterminal DesignStatementOpt;
nonterminal OptPrint;
nonterminal Statement;
nonterminal StatementOpt;
nonterminal DesignatorStatePart;
nonterminal DesignatorStatement;
nonterminal ActPars;
nonterminal ActPartsOpt;
nonterminal ArrayOpt;
nonterminal ExprOpt;
nonterminal OptFactorParenPars;
nonterminal Assignop;
nonterminal Relop;
nonterminal Addop;
nonterminal Mulop;
nonterminal VarDeclList;
nonterminal FormParsList;
nonterminal StatementCondition;
nonterminal For;
nonterminal ConditionStart;
nonterminal ConditionEnd;
nonterminal EndFor; //ovo ne treba
nonterminal GlobalEnd; //ovo ne treba
nonterminal DesignatorStatementManyClassStart;
nonterminal NamespaceFinished;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct MethodType;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ConstVal;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFact;
nonterminal rs.etf.pp1.symboltable.concepts.Struct AddTerm;
nonterminal rs.etf.pp1.symboltable.concepts.Struct CondFactOpt;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Namespace;
nonterminal rs.etf.pp1.symboltable.concepts.Obj NamespaceOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MatrixOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ArraySize;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstDeclPart;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDeclPart;


nonterminal If;
nonterminal Else;

precedence left ELSE;

// Done
Program 		    ::= (Program) PROG IDENT:name NamespaceMany NamespaceFinished ConstDeclOpt VarDeclOpt GlobalEnd MethodDeclOptMany RBRACE;

NamespaceFinished   ::= (NamespaceFinished) /* epsilon */;

// Done
GlobalEnd		    ::= (GlobalEnd) LBRACE;

Namespace 		    ::= (Namespace) NAMESPACE IDENT:name LBRACE ConstDeclOpt VarDeclOpt LBRACE MethodDeclOptMany RBRACE RBRACE;

NamespaceMany       ::= (NamespaceManyClass) NamespaceMany Namespace
                    |   (NamespaceManyEmptyClass) /* epsilon */;

MethodDeclOptMany	::= (MethodDeclOptManyClass) MethodDeclOptMany MethodDecl
					|	(MethodDeclOptEmptyClass) /* epsilon */;

// Done
ArrayOpt		    ::= (ArrayOne) LSQBRACE RSQBRACE
					|	(ArrayZero) /* epsilon */;

// Done
VarDeclPart         ::= (VarDeclPart) IDENT:name ArrayOpt;

// Done
VarDeclList         ::= (VarDeclListMany) VarDeclList COMMA VarDeclPart
                    |	(VarDeclListOne) VarDeclPart
                    |   error SEMI:l {: parser.report_error("Sintaksa greska deklaracije globalne promenljive na liniji " + lleft, null); :};

// Done
ConstDeclOpt        ::= (ConstDeclMany) ConstDeclOpt ConstDecl
					|	(ConstDeclZero) /* epsilon */;

// Done
ConstDecl           ::= (ConstDecl) CONST Type ConstDeclPart SEMI;

// Done
ConstList           ::= (ConstListMany) ConstList COMMA ConstDeclPart
                    |   (ConstListOne) ConstDeclPart;

// Done
ConstDeclPart       ::= (ConstDeclPart) IDENT:name EQUAL ConstVal:value;

// Done
VarDecl             ::= (VarDecl) Type VarDeclList SEMI;

// Done
VarDeclOpt          ::= (VarDeclMany) VarDeclOpt VarDecl
                    |   (VarDeclZero) /* epsilon */;

// Done
MethodType			::= (MethodTypeVoid) VOID
					|	(MethodTypeType) Type;

// Done
MethodDecl          ::= (MethodDecl) MethodType IDENT:name LPAREN FormParsOpt RPAREN VarDeclOpt LBRACE StatementOpt RBRACE;

// Done
FormParsOpt         ::= (FormParsOne) FormParsList
                    |   (FormParsZero) /* epsilon */;

// Done
FormParsList        ::= (FormParsListMany) FormParsList COMMA FormPars
                    |   (FormParsListOne) FormPars;

// Done
FormPars            ::= (FormPars) Type IDENT:name ArrayOpt;

// Done
Type                ::= (Type) IDENT:name;

// Done
StatementOpt        ::= (StatementMany) StatementOpt Statement
                    |   (StatementZero) /* epsilon */;

// Done
ExprOpt				::= (ExprOne) Expr
					| 	(ExprZero) /* epsilon */;

Statement           ::= (StatementDesignator) DesignatorStatement SEMI
                    |   (StatementIf) If StatementCondition Statement
                    |   (StatementIfElse) If StatementCondition Statement Else Statement
                    |   (StatementBreak) BREAK SEMI // Done
                    |   (StatementContinue) CONTINUE SEMI // Done
                    |   (StatementReturn) RETURN ExprOpt SEMI // Done
                    |   (StatementRead) READ LPAREN Designator RPAREN SEMI
                    |   (StatementPrint) PRINT LPAREN Expr OptPrint RPAREN SEMI // Done
                    |   (StatementFor) For LPAREN DesignStatementOpt ConditionStart CondFactOpt SEMI DesignStatementOpt ConditionEnd Statement EndFor // Done
                    |   (StatementMany) LBRACE StatementOpt RBRACE; // Done

// Done
StatementCondition  ::= (StatementCondition) LPAREN Condition RPAREN;

// Done
DesignStatementOpt  ::= (DesignStatementMany) DesignStatementOpt COMMA DesignatorStatement
                    |	(DesignStatementOne) DesignatorStatement
                    |	(DesignStatementZero) /* epsilon */;

// Done
If					::= (If) IF;

// Done
Else 				::= (Else) ELSE;

// Done
For 				::= (For) FOR;

// Done
EndFor              ::= (EndFor) /* epsilon */;

// Done
ConditionStart 	    ::= (ConditionStart) SEMI;

// Done
ConditionEnd        ::= (ConditionEnd) RPAREN;

// Done
CondFactOpt         ::= (CondFactOne) CondFact
                    |	(CondFactZero) /* epsilon */;

// Done
OptPrint	        ::= (PrintOne) COMMA NUMBER
					|	(PrintZero) /* epsilon */;

DesignatorStatePart ::= (DesignatorStatePartManyClass) DesignatorStatePart Designator COMMA
                    |   (DesignatorStatePartOneClass) DesignatorStatePart COMMA
                    |   (DesignatorStatePartEmptyClass) /* epsilon */;

DesignatorStatement ::= (DesignatorStatementAssignClass) Designator Assignop Expr
                    |   (DesignatorStatementParamsClass) Designator LPAREN ActPartsOpt RPAREN
                    |   (DesignatorStatementPlusClass) Designator PLUSPLUS
                    |   (DesignatorStatementMinusClass) Designator MINUSMINUS
                    |   (DesignatorStatementManyClass) DesignatorStatementManyClassStart DesignatorStatePart MUL Designator RSQBRACE Assignop Designator;

DesignatorStatementManyClassStart        ::= (DesignatorStatementManyClassStart) LSQBRACE;

// Done
ActPartsOpt	        ::= (ActPartsOne) ActPars
					|	(ActPartsZero) /* epsilon */;

// Done
ActPars             ::= (ActParsMany) ActPars COMMA Expr
                    |   (ActParsOne) Expr;

// Done
Condition           ::= (ConditionMany) Condition OR CondTerm
                    |   (ConditionOne) CondTerm;

// Done
CondTerm            ::= (CondTermManyClass) CondTerm AND CondFact
                    |   (CondTermOneClass) CondFact;

// Done
CondFact            ::= (CondFactMany) Expr Relop Expr
                    |   (CondFactOne) Expr;

// Done
AddTerm				::= (AddTermMany) AddTerm Addop Term
					|	(AddTermZero) /* epsilon */;

// Done
Expr                ::= (Expr) OptMinus Term AddTerm;

// Done
OptMinus 			::= (Minus) MINUS
					|	(NoMinus) /* epsilon */;

// Done
Term                ::= (TermMany) Term Mulop Factor
                    |   (TermOne) Factor;

// Done
ConstVal 			::= (ConstValNum) NUMBER
					|	(ConstValChar) CHAR
					|	(ConstValBoo) BOOL;

// Done
Factor              ::= (FactorParenPars) Designator OptFactorParenPars
                    |   (FactorConstVal) ConstVal
                    |   (FactorNewTypeExpr) NEW Type LSQBRACE Expr RSQBRACE
                    |   (FactorParenExpr) LPAREN Expr RPAREN;

// Done
OptFactorParenPars  ::= (ParenPars) LPAREN ActParsOpt RPAREN
                    |   (ParenParsZero) /* epsilon */;

// Done
ActParsOpt          ::= (ActParsOne) ActPars
                    |   (ActParsZero) /* epsilon */;

// Done
NamespaceOpt        ::= (WithNamespace) IDENT:namespace DOUBLECOLON IDENT:name
                    |   (NoNamespace) IDENT:name;

// Done
Designator          ::= (Designator) NamespaceOpt MatrixOpt;

// Done
MatrixOpt           ::= (MatrixMany) MatrixOpt ArraySize
                    |   (MatrixZero) /* epsilon */;

// Done
ArraySize			::= (ArraySize) LSQBRACE Expr RSQBRACE;

// Done
Assignop		    ::= (Assignop) EQUAL
                    |	error SEMI:l {: parser.report_error("Sintaksa greska dodele na liniji " + lleft, null); :};

// Done
Relop				::= (RelopEqualsto) EQUALSTO
					|	(RelopDifferent) DIFFERENT
					|	(RelopLess) LESS
					|	(RelopEless) ELESS
					|	(RelopGreater) GREATER
					|	(RelopEgreater) EGREATER;

// Done
Addop				::= (AddopPlus) PLUS
					|	(AddopMinus) MINUS;

// Done
Mulop				::= (MulopMul) MUL
					| 	(MulopDiv) DIV
					|	(MulopMod) MOD;
